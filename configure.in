dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 1.21 2013/02/26 18:41:54 tom Exp $
AC_REVISION($Revision: 1.21 $)
AC_PREREQ(2.52.20011201)
AC_INIT(add.c)
AC_CONFIG_HEADER(config.h:config_h.in)

CF_CHECK_CACHE

AC_ARG_PROGRAM

CF_PROG_CC
AC_PROG_INSTALL

CF_MAKE_TAGS
CF_PROG_LINT

AC_HEADER_STDC
AC_CHECK_HEADERS(\
getopt.h \
stdlib.h \
string.h \
unistd.h \
)

CF_ENABLE_WARNINGS

CF_XOPEN_SOURCE
CF_WITH_CURSES_DIR

my_curses=no
AC_ARG_WITH(ncurses,
	[  --with-ncurses          compile/link with ncurses library],
	[my_curses=ncurses])
AC_ARG_WITH(ncursesw,
	[  --with-ncursesw         compile/link with wide-char ncurses library],
	[my_curses=ncursesw])
AC_ARG_WITH(pdcurses,
	[  --with-pdcurses         compile/link with PDCurses library],
	[my_curses=pdcurses])
if test $my_curses = pdcurses ; then
	CF_PDCURSES_X11
elif test $my_curses != no ; then
	if test $my_curses = ncursesw ; then
		CF_UTF8_LIB
	fi
	CF_NCURSES_CONFIG($my_curses)
else
	CF_CURSES_CONFIG
fi

CF_DISABLE_LEAKS

AC_TYPE_SIZE_T

AC_MSG_CHECKING(declaration of chtype)
AC_CACHE_VAL(ac_have_type_chtype,[
	AC_TRY_COMPILE([#include <curses.h>],
		[chtype x = 1],
		[ac_have_type_chtype=yes],
		[ac_have_type_chtype=no])])
AC_MSG_RESULT($ac_have_type_chtype)
test $ac_have_type_chtype = no && AC_DEFINE(chtype,int,[Define to the type used for curses character+attribute])

AC_CHECK_FUNCS(\
getcwd \
getopt \
)

CF_CURSES_FUNCS(\
COLOR_PAIR \
beep \
bkgd \
define_key \
flash \
keypad \
typeahead \
wsetscrreg \
)

###	output makefile and config.h
AC_OUTPUT(makefile,,,cat)
